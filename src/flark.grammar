//!element

@top Tree { element }

element {
    VBox { (VPipe item)+ }
  | HBox { (HPipe item)+ }
}

item {
    String lineEnd
  | lineEnd Block
}

//!block

Block { indent element (dedent | eof) }

//!skip

@skip {
  spaces |
  Comment |
  blankLineStart (spaces | Comment)* lineEnd
}

//!lineEnd

lineEnd { newline | eof }

//!context

@context trackIndent from "./tokens.js"

//!externalTokens

@external tokens indentation from "./tokens.js" {
  indent
  dedent
  blankLineStart
}

//!tokens

@tokens {
  spaces { $[ \t]+ }
  newline { "\n" }
  eof { @eof }
  Comment { "#" ![\n]+ }
  String { ![ \t\n#] ![\n]* }
  VPipe { "|" }
  HPipe { "-" }
}

@external propSource flarkHighlighting from "./highlight"