//!element

@top Tree { element }

element {
    VBox { (VPipe Item)+ }
  | HBox { (HPipe Item)+ }
}

Item {
    template? lineEnd Block?
}

//!block

Block { indent element (dedent | eof) }

template { (
    PlainText
  | InsertBlock
  | StyleBlock
)+ }

InsertBlock {
  LBrace
  expression
  RBrace
}

expression {
    Identifier
  | Number
}

StyleBlock {
  LBracket
  StyleExpr+
  RBracket
}

StyleExpr {
    styleAtom (Dot styleAtom)*
}

styleAtom {
    Identifier
  | Number
  | InsertBlock
}

//!skip

@skip {
  spaces |
  Comment |
  blankLineStart (spaces | Comment)* lineEnd
}

//!lineEnd

lineEnd { newline | eof }

//!context

@context trackIndent from "./tokens.js"

//!externalTokens

@external tokens indentation from "./tokens.js" {
  indent
  dedent
  blankLineStart
}

//!tokens

@tokens {
  spaces { $[ \t]+ }
  newline { "\n" }
  eof { @eof }
  Comment { "#" ![\n]+ }
  PlainText { ![ \t\n#{[] ![\n{[]* }
  VPipe { "|" }
  HPipe { "-" }
  Identifier { $[a-zA-Z_] $[a-zA-Z_0-9]* }
  Number { "-"? @digit+ ("." @digit+)? }
  LBrace { "{" }
  RBrace { "}" }
  LBracket { "[" }
  RBracket { "]" }
  Dot { "." }
}


@external propSource flarkHighlighting from "./highlight"